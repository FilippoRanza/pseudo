
use crate::ast;

#[LALR]
grammar;


pub Code : ast::Code = {
    caption_kw <caption: Text> open_bracket <decl: DeclList> close_bracket open_brace <code: PCode> close_brace => ast::Code{<>}
}

PCode: Vec<ast::Command> = {
    <cmd: (Command)+> => cmd
}

Command: ast::Command = {
    <blk: AssignBlock> => ast::Command::Assign(blk),
    <blk: CondBlock> => ast::Command::Condition(blk),
    <blk: ForBlock> => ast::Command::ForLoop(blk),
    <blk: WhileBlock> => ast::Command::WhileLoop(blk),
    <blk: ReturnBlock> => ast::Command::Return(blk),
}

WhileBlock: ast::ConditionPair = {
    while_kw <cond: Text> open_brace <body: PCode> close_brace => ast::ConditionPair {<>}
}

ReturnBlock: String = {
    return_kw <Text> semicolon
}


DeclList: ast::DeclBlock = {
    <list: (<Decl> comma)*> <d: Decl> => {
        let mut list =  list;
        list.push(d);
        list
    }
}

Decl: ast::Decl = {
    <n: Text> colon <t: Text> => (n, t)
}


AssignBlock: ast::Assign = {
    <lhs: Text> eq <rhs: Text> semicolon => (lhs, rhs),
    <lhs: Text> eq_decl <rhs: Text> semicolon => (lhs, rhs),
    <lhs: Text> eq_arrow <rhs: Text> semicolon => (lhs, rhs),
}


CondBlock: ast::Condition = {
    <if_block: IfBlock>
    <elif_blocks: ElifBlockList> 
    <else_block: (ElseBlock)?> => ast::Condition{<>}
}

IfBlock: ast::ConditionPair = {
    if_kw <cond: Text> open_brace <body: PCode> close_brace => ast::ConditionPair{<>}
}

ElifBlockList: Vec<ast::ConditionPair> = {
    <(ElifBlock)*>
}

ElifBlock: ast::ConditionPair = {
    elif_kw <cond: Text> open_brace <body: PCode> close_brace => ast::ConditionPair{<>}
}

ElseBlock: Vec<ast::Command> = {
    else_kw open_brace <PCode> close_brace
}


ForBlock: ast::ForLoop = {
    <IterForLoop>,
    <CountForLoop>
}

IterForLoop: ast::ForLoop = {
    for_kw <name: Text> in_kw <iter: Text> open_brace <body: PCode> close_brace 
    => ast::ForLoop { 
        kind: ast::ForLoopKind::Iter((name, iter)),
        body
    }
}

CountForLoop:  ast::ForLoop = {
    for_kw <text: Text> eq 
    <begin: Text> colon <close_brace: Text> 
    open_brace <body: PCode> close_brace 
    => ast::ForLoop { 
        kind: ast::ForLoopKind::Count((text, begin, close_brace)),
        body
    }
}

open_brace = "{";
close_brace = "}";

open_bracket = "(";
close_bracket = ")";

caption_kw = "func";
code_kw = "code";
return_kw = "return";
semicolon = ";";
while_kw = "while";
comma = ",";
eq = "=";
eq_decl = ":=";
eq_arrow = "<-";
colon = ":";
if_kw = "if";
elif_kw = "elif";
else_kw = "else";
in_kw = "in";
for_kw = "for";

Text: String = {
    <list: (Token)+> => {
        list.join(" ")
    }
}

Token: &'input str = <r"[A-Za-z0-9\+\-\*/<>\\]+">;

match {
    r#"\s+"# => {},
    r#"/\*([^*]|\*[^/])*\*/"# => {},
    r#"//[^\n]+"# => {},

    r"\{" => "{",
    r"\(" => "(",
    r"\)" => ")",
    r"\}" => "}",
    r"," => ",",
    r":" => ":",
    r"=" => "=",
    r":=" => ":=",
    r"<-" => "<-",
    r";" => ";",

    r"in" => "in",
    r"func" => "func",
    r"code" => "code",
    r"for" => "for",
    r"while" => "while",
    r"if" => "if",
    r"elif" => "elif",
    r"else" => "else",
    r"return" => "return",
    
} else {
    r"[A-Za-z0-9\+\-\*/<>\\]+"
}
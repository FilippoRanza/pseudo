
use crate::ast;

#[LALR]
grammar;


pub Code : ast::Code = {
    caption_kw <caption: Text> open_bracket <decl: DeclList> close_bracket open_brace <code: PCode> close_brace => ast::Code{<>}
}

PCode: Vec<ast::Command> = {
    <cmd: (Command)+> => cmd
}

Command: ast::Command = {
    <blk: AssignBlock> => ast::Command::Assign(blk),
    <blk: CondBlock> => ast::Command::Condition(blk),
    <blk: ForBlock> => ast::Command::ForLoop(blk),
    <blk: WhileBlock> => ast::Command::WhileLoop(blk),
    <blk: ReturnBlock> => ast::Command::Return(blk),
    <blk: FunctionBlock> => ast::Command::Function(blk)
}

FunctionBlock: ast::Function = {
    <Function> semicolon 
}

Function: ast::Function = {
    <name: Text> open_bracket <args: NameList> close_bracket => ast::Function{<>}
}

CodeType: ast::CodeType = {
    <name: Text> => ast::CodeType::Name(name),
    <func: Function> => ast::CodeType::Func(func)
}

NameList: Vec<String> = {
    <names: (<Text> comma)*> <name: (Text)?> => {
        match name {
            Some(name) => {
                let mut names = names;
                names.push(name);
                names
            },
            None => names
        }
    }
}


WhileBlock: ast::ConditionPair = {
    while_kw <cond: CodeType> open_brace <body: PCode> close_brace => ast::ConditionPair {<>}
}

ReturnBlock: ast::CodeType = {
    return_kw <CodeType> semicolon
}


DeclList: ast::DeclBlock = {
    <list: (<Decl> comma)*> <d: (Decl)?> => {
        match d {
            Some(d) => {
                let mut list =  list;
                list.push(d);
                list
            },
            None => list
        }
    }
}

Decl: ast::Decl = {
    <n: Text> colon <t: Text> => (n, t)
}


AssignBlock: ast::Assign = {
    <lhs: Text> eq <rhs: CodeType> semicolon => (lhs, rhs),
    <lhs: Text> eq_decl <rhs: CodeType> semicolon => (lhs, rhs),
    <lhs: Text> eq_arrow <rhs: CodeType> semicolon => (lhs, rhs),
}


CondBlock: ast::Condition = {
    <if_block: IfBlock>
    <elif_blocks: ElifBlockList> 
    <else_block: (ElseBlock)?> => ast::Condition{<>}
}

IfBlock: ast::ConditionPair = {
    if_kw <cond: CodeType> open_brace <body: PCode> close_brace => ast::ConditionPair{<>}
}

ElifBlockList: Vec<ast::ConditionPair> = {
    <(ElifBlock)*>
}

ElifBlock: ast::ConditionPair = {
    elif_kw <cond: CodeType> open_brace <body: PCode> close_brace => ast::ConditionPair{<>}
}

ElseBlock: Vec<ast::Command> = {
    else_kw open_brace <PCode> close_brace
}


ForBlock: ast::ForLoop = {
    <IterForLoop>,
    <CountForLoop>
}

IterForLoop: ast::ForLoop = {
    for_kw <name: Text> in_kw <iter: CodeType> open_brace <body: PCode> close_brace 
    => ast::ForLoop { 
        kind: ast::ForLoopKind::Iter((name, iter)),
        body
    }
}

CountForLoop:  ast::ForLoop = {
    for_kw <text: Text> eq 
    <begin: CodeType> colon <close_brace: CodeType> 
    open_brace <body: PCode> close_brace 
    => ast::ForLoop { 
        kind: ast::ForLoopKind::Count((text, begin, close_brace)),
        body
    }
}

open_brace = "{";
close_brace = "}";

open_bracket = "(";
close_bracket = ")";

caption_kw = "func";
code_kw = "code";
return_kw = "return";
semicolon = ";";
while_kw = "while";
comma = ",";
eq = "=";
eq_decl = ":=";
eq_arrow = "<-";
colon = ":";
if_kw = "if";
elif_kw = "elif";
else_kw = "else";
in_kw = "in";
for_kw = "for";

Text: String = {
    <list: (Token)+> => {
        list.join(" ")
    }
}

Token: &'input str = <r"[A-Za-z0-9\+\-\*/<>\\\$\{\}_\.]+">;

match {
    r#"\s+"# => {},
    r#"/\*([^*]|\*[^/])*\*/"# => {},
    r#"//[^\n]+"# => {},

    r"\{" => "{",
    r"\(" => "(",
    r"\)" => ")",
    r"\}" => "}",
    r"," => ",",
    r":" => ":",
    r"=" => "=",
    r":=" => ":=",
    r"<-" => "<-",
    r";" => ";",

    r"in" => "in",
    r"func" => "func",
    r"code" => "code",
    r"for" => "for",
    r"while" => "while",
    r"if" => "if",
    r"elif" => "elif",
    r"else" => "else",
    r"return" => "return",
    
} else {
    r"[A-Za-z0-9\+\-\*/<>\\\$\{\}_\.]+"
}